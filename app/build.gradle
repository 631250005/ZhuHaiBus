apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        println "===================error code!"
        return 1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'//, '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        println "===================error name!"
        return "1.0.0"
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    flavorDimensions 'internal'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.scrat.zhuhaibus"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode getVersionCode()
        versionName getVersionName() + "." + getVersionCode()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary= true
        manifestPlaceholders = [CHANNEL_NAME: "default", UMENG_APPKEY: UMENG_APPKEY, APP_NAME: "晴天公交(珠海)"]
    }

    signingConfigs {
        config {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            try {
                signingConfig signingConfigs.config
            } catch (ignore) {
            }
        }
        debug {
            try {
                signingConfig signingConfigs.config
            } catch (ignore) {
            }
        }
    }

    productFlavors {
        gp {
            dimension 'internal'
            manifestPlaceholders = [CHANNEL_NAME: "gp", APP_NAME: "珠海公交"]
        }
        oppo {
            dimension 'internal'
            manifestPlaceholders = [CHANNEL_NAME: "oppo"]
        }
        xiaomi {
            dimension 'internal'
            manifestPlaceholders = [CHANNEL_NAME: "xiaomi", APP_NAME: "珠海公交"]
        }
        h5 {
            dimension 'internal'
            manifestPlaceholders = [CHANNEL_NAME: "h5"]
        }
        _360 {
            dimension 'internal'
            manifestPlaceholders = [CHANNEL_NAME: "_360"]
        }
//        yingyongbao {
//            dimension 'internal'
//            manifestPlaceholders = [CHANNEL_NAME: "yingyongbao"]
//        }
//        huawei {
//            dimension 'internal'
//            manifestPlaceholders = [CHANNEL_NAME: "huawei"]
//        }
    }

//    android.applicationVariants.all { variant ->
//        variant.outputs.all { output ->
//            def file = output.outputFile
//            def stdout = new ByteArrayOutputStream()
//            exec {
//                commandLine 'git', 'describe', '--abbrev=0', '--tags'
//                standardOutput = stdout
//            }
//
//            def vn = stdout.toString().trim()
//            outputFileName = new File("${project.projectDir}/../outputs/apks/" + variant.name, "${project.projectDir}/../outputs/apks/" + file.name.replace(".apk", "-" + vn + ".apk"))
//        }
//    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-vector-drawable:27.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

//    compile 'com.google.code.gson:gson:2.8.2'
//    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'io.reactivex:rxandroid:1.0.1'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.4.0'
    implementation 'com.artemzin.rxjava:proguard-rules:1.1.3.0'

    implementation 'com.github.bumptech.glide:glide:4.5.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0'

    implementation 'com.umeng.analytics:analytics:latest.integration'
    implementation 'com.google.android.gms:play-services-ads:11.2.0'
}
